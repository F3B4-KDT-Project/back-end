plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.github.ben-manes.versions' version '0.48.0'
	id 'com.google.cloud.tools.jib' version '3.4.4' // jib
	id 'com.diffplug.spotless' version '6.23.3' // spotless

}

group = 'kdt'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('springCloudVersion', "2023.0.3")
	springAiVersion = "1.0.0-M4"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "org.springframework.ai:spring-ai-bom:$springAiVersion"
	}
}

spotless {
	java {
		// Google Java 포맷 적용
		googleJavaFormat()
		// 아래 순서로 import문 정렬
		importOrder('java', 'javax', 'jakarta', 'org', 'com')
		// 사용하지 않는 import 제거
		removeUnusedImports()
		// 각 라인 끝에 있는 공백을 제거
		trimTrailingWhitespace()
		// 파일 끝에 새로운 라인 추가
		endWithNewline()
	}
}

jib {
	from {
		image = "openjdk:17-jdk"
	}
	to {
		image = "groomkdt/kdt"
		tags = ["latest"]
		auth {
			username = 'groomkdt'
			password = System.getenv('DOCKERHUB_PASSWORD') ?: ''
		}
	}
	container {
		jvmFlags = ["-Xms128m", "-Xmx128m", "-Dspring.profiles.active=dev", "-Dfile.encoding=UTF-8"]
		ports = ["8080"]
		allowInsecureRegistries = true
	}
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// jwt
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.2'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.2'

	// swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'

	// mysql
	runtimeOnly 'com.mysql:mysql-connector-j'

	// websocket
	implementation 'org.springframework.boot:spring-boot-starter-websocket'

	implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'

	//s3
	implementation 'io.awspring.cloud:spring-cloud-aws-starter:3.1.1'
	implementation 'software.amazon.awssdk:s3:2.20.40'

	// oauth
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// batch
	implementation 'org.springframework.boot:spring-boot-starter-batch'
	implementation'org.springframework.boot:spring-boot-starter-quartz'

	// actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'io.micrometer:micrometer-registry-prometheus'
}

tasks.named('test') {
	useJUnitPlatform()
}


tasks.named('compileJava') {
	dependsOn 'spotlessApply'
	dependsOn 'addGitHooks'
}

tasks.register('addGitHooks', Copy) {
	from './script/pre-commit'
	into './.git/hooks'
	filePermissions {
		unix('r-xr-xr-x')
	}
}
